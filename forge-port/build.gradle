plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.140'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

neoForge {
    version = neo_version

    runs {
        client {
            client()
        }
        server {
            server()
            programArgument '--nogui'
        }
        data {
            data()
            programArguments.addAll '--mod', mod_id, '--all',
                    '--output', file('src/generated/resources/').absolutePath,
                    '--existing', file('src/main/resources/').absolutePath
        }
    }

    mods {
        "reiparticlesapi" {
            sourceSet(sourceSets.main)
        }
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

repositories {
    maven { url = 'https://maven.neoforged.net/releases' }
    mavenCentral()
}

dependencies {
    // ClassGraph â€” runtime classpath scanning (used by ReiAPIScanner)
    implementation 'io.github.classgraph:classgraph:4.8.174'

    testImplementation platform('org.junit:junit-bom:5.11.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    var replaceProperties = [
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            minecraft_version_range: minecraft_version_range
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : 'reiasu',
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.version,
                'Implementation-Vendor'   : 'reiasu'
        ])
    }
    into('META-INF') {
        from rootProject.file('../LICENSE')
        from rootProject.file('../NOTICE')
        from rootProject.file('../THIRD_PARTY_NOTICES.md')
    }
    into('META-INF/LICENSES') {
        from rootProject.file('../LICENSES/LGPL-3.0.txt')
        from rootProject.file('../LICENSES/GPL-3.0.txt')
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}
